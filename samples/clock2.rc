// A version of the Clock automaton of Distributed Algorithms, p. 213.
// This implementation uses synchronous getters to increase parallelism.

type Counter component {
  counter uint;
};

action (this +const * Counter) (true) {
  activate {
    this.counter++;
    println (`Counter is now `, this.counter);
  };
}

func (this +const * Counter) Counter () uint {
  return this.counter;
}

type Clock component {
  counter Counter;
  flag bool;
  response push (t uint);
};

reaction (this +const * Clock) request () {
  activate {
    this.flag = true;
  };
}

action (this +const * Clock) (this.flag) {
  activate response (this.counter.Counter()) {
    this.flag = false;
  };
}

type Sampler component {
  flag bool;
  request push ();
};

action (this +const * Sampler) (!this.flag) {
  activate request () {
    this.flag = true;
  };
}

reaction (this +const * Sampler) response (t uint) {
  println (`Received `, t);
  activate {
    this.flag = false;
  };
}

type System component {
  sampler Sampler;
  clock Clock;
};

init (this *System) Initially ()
{ }

bind (this *System) {
  this.sampler.request -> this.clock.request;
  this.clock.response -> this.sampler.response;
}

instance s System Initially ();
